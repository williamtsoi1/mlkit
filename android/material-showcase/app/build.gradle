apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.google.mlkit.md"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/INDEX.LIST'
        exclude("META-INF/*.kotlin_module")
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField 'String', 'GOOGLE_CLOUD_API_KEY', GoogleCloudApiKey
            it.buildConfigField 'String', 'GOOGLE_CLOUD_PROJECT', GoogleCloudProject
            it.buildConfigField 'String', 'GOOGLE_CLOUD_REGION', GoogleCloudRegion
            it.buildConfigField 'String', 'GOOGLE_CLOUD_VISION_PRODUCT_SET', GoogleCloudVisionProductSet
            it.buildConfigField 'String', 'GOOGLE_CLOUD_VISION_PRODUCT_CATEGORY', GoogleCloudVisionProductCategory
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    aaptOptions {
        noCompress "tflite"
    }

    sourceSets.main {
        assets.srcDirs = ['assets']
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.android.material:material:1.2.0'
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.core:core-ktx:1.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation 'androidx.preference:preference:1.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.72"

    // Barcode model
    implementation 'com.google.mlkit:barcode-scanning:16.0.3'

    // Object feature and model
    implementation 'com.google.mlkit:object-detection:16.2.1'
    // Custom model
    implementation 'com.google.mlkit:object-detection-custom:16.2.1'

    // GCP SDK
    implementation 'com.google.cloud:google-cloud-vision:1.100.10'
    implementation 'com.google.apis:google-api-services-vision:v1-rev451-1.25.0'

    compile 'com.google.api-client:google-api-client-android:1.23.0' exclude module: 'httpclient'
    compile 'com.google.http-client:google-http-client-gson:1.23.0' exclude module: 'httpclient'

    api 'com.google.guava:guava:27.1-jre'
}
